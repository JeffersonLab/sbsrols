# 
#  Test readout list for CODA 3
#
#    David Abbott, JLAb 2013

readout list FRED
maximum 40960,100
polling
test readout

int blklevel = 1;
int maxdummywords = 200;

#  Type 0xff10 is RAW trigger No timestamps
#  Type 0xff11 is RAW trigger with timestamps (64 bits)
int trigBankType = 0xff11;


begin download


  log inform "User Download Executed"

end download
    
begin prestart

  variable jj

  log inform "Entering User Prestart"

  init trig source TEST
  link sync trig source TEST 1 to usrtrig and usrtrig_done
  event type 1 then read TEST 1

# Prescale of 10000 gives about 300 Hz
  TESTflag = 0;
  TEST_prescale = 10;

  log inform "User Prestart Executed"

end prestart

begin end

  TESTflag = 0;

  log inform "User End Executed"

end end

begin pause

  TESTflag = 0;

  log inform "User Pause Executed"

end pause

begin go
  
  log inform "Entering User Go"
  
  TESTflag = 1;

end go

begin trigger usrtrig
 
  variable ii, evtnum

  evtnum = *(rol->nevents);

# Open ROC raw bank;
 CEOPEN(ROCID,BT_BANK,blklevel);

#Create Dummy trigger Bank
%%
CBOPEN(trigBankType,BT_SEG,blklevel);
for(ii=0;ii<blklevel;ii++) {
   if(trigBankType == 0xff11) {
     *rol->dabufp++ = (EVTYPE<<24)|(0x01<<16)|(3);
   }else{
     *rol->dabufp++ = (EVTYPE<<24)|(0x01<<16)|(1);
   }
   *rol->dabufp++ = (blklevel*(evtnum-1) + (ii+1));
   if(trigBankType == 0xff11) {
      *rol->dabufp++ = 0x12345678;
      *rol->dabufp++ = 0;
   }
}
CBCLOSE;
%%

#Create ROC data Bank with dummy data
CBOPEN(1,BT_UI4,blklevel);
  output hex da000011
%%
  for(ii=0;ii<maxdummywords;ii++) {
     *rol->dabufp++ = ii;
  }
%%
  output hex da0000ff
CBCLOSE;

CECLOSE;

end trigger

begin done usrtrig

end done

begin done

end done

begin __reset
%%
/* This is a test */
%%
end __reset


